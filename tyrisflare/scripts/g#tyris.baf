IF // Set variable as we can't use NumTimesSpokenTo() if we replace her CRE
  NumTimesTalkedToGT(0)
  !Global("G#TF.HasBeenSpokenTo","GLOBAL",1)
THEN
  RESPONSE #1
    SetGlobal("G#TF.HasBeenSpokenTo","GLOBAL",1)
END

IF // Set variable to prevent further replacement of her CRE
  InParty(Myself)
  !Global("G#TF.TyrisJoined","GLOBAL",1)
THEN
  RESPONSE #1
    SetGlobal("G#TF.TyrisJoined","GLOBAL",1)
END

IF // Set romance active if <CHARNAME> is a match
  Global("G#TF.Lovetalk","LOCALS",0)
  Global("G#TF.TyrisMatch","GLOBAL",1)
THEN
  RESPONSE #1
    RealSetGlobalTimer("G#TF.TyrisRomance","GLOBAL",1800)
    SetGlobal("G#TF.Lovetalk","LOCALS",1)
    SetGlobal("G#TF.TyrisRomanceActive","GLOBAL",1)
END

IF // Kill Aerie romance if Tyris romance becomes serious
  InParty(Myself)
  Global("G#TF.TyrisRomanceActive","GLOBAL",2)
  OR(2)
    Global("AerieRomanceActive","GLOBAL",1)
    Global("AerieRomanceActive","GLOBAL",2)
THEN
  RESPONSE #1
    SetGlobal("AerieRomanceActive","GLOBAL",3)
END

IF // Kill Jaheira romance if Tyris romance becomes serious
  InParty(Myself)
  Global("G#TF.TyrisRomanceActive","GLOBAL",2)
  OR(2)
    Global("JaheiraRomanceActive","GLOBAL",1)
    Global("JaheiraRomanceActive","GLOBAL",2)
THEN
  RESPONSE #1
    SetGlobal("JaheiraRomanceActive","GLOBAL",3)
END

IF // Kill Viconia romance if Tyris romance becomes serious
  InParty(Myself)
  Global("G#TF.TyrisRomanceActive","GLOBAL",2)
  OR(2)
    Global("ViconiaRomanceActive","GLOBAL",1)
    Global("ViconiaRomanceActive","GLOBAL",2)
THEN
  RESPONSE #1
    SetGlobal("ViconiaRomanceActive","GLOBAL",3)
END

IF // Kill Neera romance if Tyris romance becomes serious
  InParty(Myself)
  Global("G#TF.TyrisRomanceActive","GLOBAL",2)
  OR(2)
    Global("NeeraRomanceActive","GLOBAL",1)
    Global("NeeraRomanceActive","GLOBAL",2)
THEN
  RESPONSE #1
    SetGlobal("NeeraRomanceActive","GLOBAL",3)
END

IF // Kill Dorn romance if Tyris romance becomes serious
  InParty(Myself)
  Global("G#TF.TyrisRomanceActive","GLOBAL",2)
  OR(2)
    Global("DornRomanceActive","GLOBAL",1)
    Global("DornRomanceActive","GLOBAL",2)
THEN
  RESPONSE #1
    SetGlobal("DornRomanceActive","GLOBAL",3)
END

IF // Trigger "morning after" lovetalks
  InParty(Myself)
  PartyRested()
  OR(2)
    Global("G#TF.TyrisRomanceActive","GLOBAL",1)
    Global("G#TF.TyrisRomanceActive","GLOBAL",2)
  Global("G#TF.Lovetalk","LOCALS",39) // "Mmm... morning"
THEN
  RESPONSE #1
    IncrementGlobal("G#TF.Lovetalk","LOCALS",1)
    PlaySong(0)
    PlaySound("G#TFSONG")
    Interact(Player1)
END 

IF // Trigger lovetalks
  InParty(Myself)
  RealGlobalTimerExpired("G#TF.TyrisRomance","GLOBAL")
  OR(2)
    Global("G#TF.TyrisRomanceActive","GLOBAL",1)
    Global("G#TF.TyrisRomanceActive","GLOBAL",2)
  Global("G#TF.TyrisMatch","GLOBAL",1)
  !AreaType(DUNGEON)
  !AreaCheck("AR1300")
  See(Player1)
  !StateCheck("G#Tyris",CD_STATE_NOTVALID)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  !See([ENEMY])
  !Range([NEUTRAL],10)
  CombatCounter(0)
  OR(18)
    Global("G#TF.Lovetalk","LOCALS",1) // "What made you become an adventurer?"
    Global("G#TF.Lovetalk","LOCALS",3) // "Did you get revenge on Sarevok?"
    Global("G#TF.Lovetalk","LOCALS",7) // "A page fell out of your diary..."
    Global("G#TF.Lovetalk","LOCALS",9) // "Who was Bhaal?"
    Global("G#TF.Lovetalk","LOCALS",11) // "What was Candlekeep like?"
    Global("G#TF.Lovetalk","LOCALS",13) // "Aren't you curious about my warpaint?"
    Global("G#TF.Lovetalk","LOCALS",15) // "Have you heard of the Golden Axe?"
    Global("G#TF.Lovetalk","LOCALS",19) // "I feel guilty about my companions..."
    Global("G#TF.Lovetalk","LOCALS",21) // "I'm not used to walking everywhere..."
    Global("G#TF.Lovetalk","LOCALS",23) // "Do you remember the first time you killed a man?"
    Global("G#TF.Lovetalk","LOCALS",25) // "How do you want to die?"
    Global("G#TF.Lovetalk","LOCALS",27) // "Sorry for the teary-eyed nonsense"
    Global("G#TF.Lovetalk","LOCALS",29) // "Do you think I should cut my hair?"
    Global("G#TF.Lovetalk","LOCALS",31) // "How do you cure the hiccups?"
    Global("G#TF.Lovetalk","LOCALS",35) // "Parlour tricks"
    Global("G#TF.Lovetalk","LOCALS",41) // "I lied..."
    Global("G#TF.Lovetalk","LOCALS",43) // "Have you ever died?"
    Global("G#TF.Lovetalk","LOCALS",45) // "There's something I want you to have..."
THEN
  RESPONSE #1
    IncrementGlobal("G#TF.Lovetalk","LOCALS",1)
    PlaySong(0)
    PlaySound("G#TFSONG")
    Interact(Player1)
END

IF // Start timer for first Tyris-initiated flirt
  InParty(Myself)
  Global("G#TF.StartTyrisFlirts","LOCALS",0)
  OR(2)
    Global("G#TF.TyrisRomanceActive","GLOBAL",1)
    Global("G#TF.TyrisRomanceActive","GLOBAL",2)
  GlobalGT("G#TF.Lovetalk","LOCALS",10)
THEN
  RESPONSE #1
    SetGlobal("G#TF.StartTyrisFlirts","LOCALS",1)
    RealSetGlobalTimer("G#TF.TyrisFlirt","LOCALS",900)
END

IF // Trigger Tyris-initiated flirts
  InParty(Myself)
  See(Player1)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  !StateCheck("G#Tyris",CD_STATE_NOTVALID)
  CombatCounter(0)
  RealGlobalTimerExpired("G#TF.TyrisFlirt","LOCALS")
  Global("G#TF.TyrisFlirtNow","LOCALS",0)
  OR(2)
    Global("G#TF.TyrisRomanceActive","GLOBAL",1)
    Global("G#TF.TyrisRomanceActive","GLOBAL",2)
  GlobalGT("G#TF.Lovetalk","LOCALS",10)
  !Global("G#TF.Lovetalk","LOCALS",41)
  !AreaCheck("AR2100")
  !AreaCheck("AR2101")
  !AreaCheck("AR2102")
  !AreaCheck("AR2200")
  !AreaCheck("AR2201")
  !AreaCheck("AR2202")
  !AreaCheck("AR2203")
  !AreaCheck("AR2204")
  !AreaCheck("AR2205")
  !AreaCheck("AR2206")
  !AreaCheck("AR2207")
  !AreaCheck("AR2208")
  !AreaCheck("AR2209")
  !AreaCheck("AR2210")
  !AreaCheck("AR2400")
  !AreaCheck("AR2401")
  !AreaCheck("AR2402")
THEN
  RESPONSE #1
    SetGlobal("G#TF.TyrisFlirtNow","LOCALS",1)
    StartDialogueNoSet(Player1)
    Wait(1)
END

IF // Post-vampire talk
  !InParty(Myself)
  Global("G#TF.TyrisVampire","GLOBAL",4)
THEN
  RESPONSE #100
    SetGlobal("G#TF.TyrisVampire","GLOBAL",5)
    StartDialogueNoSet(Player1)
END

IF // Sex with Phaere
  InParty(Myself)
  See(Player1)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  OR(2)
    Global("PhaereInnuendo","GLOBAL",2)
    Global("PhaereInnuendo","GLOBAL",3)
  !Range("Phaere",6)
  Global("G#TF.SexWithPhaereTalk","LOCALS",0)
  OR(2)
    Global("G#TF.TyrisRomanceActive","GLOBAL",1)
    Global("G#TF.TyrisRomanceActive","GLOBAL",2)
THEN
  RESPONSE #1
    SetGlobal("G#TF.SexWithPhaereTalk","LOCALS",1)
    Dialogue(Player1)
END

IF // Trigger string fixer
  HotKey(F)
THEN
  RESPONSE #1
    ClearActions("G#Tyris")
    StartCutSceneMode()
    StartCutScene("G#FIX")
END
